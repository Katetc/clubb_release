!----------------------------------------------------------------------
! $Id$
module cobra
  !       Description:
  !       Contains subroutines for the COBRA CO2 case.
  !----------------------------------------------------------------------

  implicit none

  private ! Default Scope

  public :: cobra_sfclyr

  contains

  !-----------------------------------------------------------------------
  subroutine cobra_sfclyr( time, z, rho_sfc, thlm_sfc, ubar, & 
                           wpthlp_sfc, wprtp_sfc, ustar, & 
                           wpsclrp_sfc, wpedsclrp_sfc, T_sfc )

  !       Description:
  !       This subroutine computes surface fluxes of horizontal momentum,
  !       heat and moisture according to the format used for the GCSS ARM 
  !       case.

  !       Notes:
  !       The data has been altered so it can be used for the COBRA CO2 
  !       case.
  !-----------------------------------------------------------------------

  use constants_clubb, only: grav ! Variable(s)

  use parameters_model, only: sclr_dim, edsclr_dim ! Variable(s)

  use interpolation, only: factor_interp

  use stats_precision, only: time_precision ! Variable(s)

  use diag_ustar_module, only: diag_ustar ! Variable(s)

  use surface_flux, only: convert_sens_ht_to_km_s, convert_latent_ht_to_m_s ! Procedure(s)

  use array_index, only: &
    iisclr_rt, iisclr_thl, iisclr_CO2, & ! Variable(s)
    iiedsclr_rt, iiedsclr_thl, iiedsclr_CO2

  use time_dependent_input, only: latent_ht_given, sens_ht_given, time_sfc_given, &
                                  CO2_sfc_given, T_sfc_given,& ! Variable (s)
                                  time_select ! Procedure(s)

  implicit none

  intrinsic :: sqrt, max

  ! Parameter Constants
  real, parameter :: & 
    M_da  = 0.02897  ! Molecular weight of dry air.
  integer, parameter :: &
    ntimes = 49

  ! Input variables
  real(kind=time_precision), intent(in) ::  & 
    time      ! Current time                [s]

  real, intent(in) :: & 
    z,         & ! Elevation at zt=2           [m]
    rho_sfc,       & ! Air density at surface      [kg/m^3]
    thlm_sfc,  & ! Theta_l at zt(2)            [K]
    ubar         ! mean sfc wind speed         [m/s]

  ! Output variables
  real, intent(out) ::  & 
    wpthlp_sfc,  & ! w'theta_l' surface flux   [(m K)/s]
    wprtp_sfc,   & ! w'rt' surface flux        [(m kg)/(kg s)]
    ustar,       & ! surface friction velocity [m/s]
    T_sfc          ! Temperature at the surface [K]

  ! Output variables
  real, intent(out), dimension(sclr_dim) ::  & 
    wpsclrp_sfc    ! w'sclr' surface flux          [units m/s]

  real, intent(out), dimension(edsclr_dim) ::  & 
    wpedsclrp_sfc  ! w' edsclr' surface flux       [units m/s]

  ! Local variables
  integer :: &
    before_time, after_time
  real ::  &  
    heat_flx, moisture_flx, &                 ! [W/m^2]
    heat_flx2, moisture_flx2, &
    time_frac, &
    bflx
  real :: CO2_flx
  real :: CO2_flx2

  ! COBRA roughness height
  ! real, parameter :: z0 = 0.035  ! ARM momentum roughness height
  real, parameter :: z0 = 1.75   ! momentum roughness height

  !-----------------BEGIN CODE-------------------------

  ! Default Initialization
  heat_flx = 0.0
  moisture_flx = 0.0
  CO2_flx = 0.0
  CO2_flx2 = 0.0 ! Default initialization

  ! Compute heat and moisture fluxes from ARM data in (W/m2)

  ! Use time_select to caluclate the indexes before and after time
  ! and the time fraction necessary for factor_interp
  call time_select( time, ntimes, time_sfc_given, &
                    before_time, after_time, time_frac )

  ! Interpolate fluxes
  heat_flx = factor_interp( time_frac, sens_ht_given(after_time), &
                                       sens_ht_given(before_time) )
  moisture_flx = factor_interp( time_frac, latent_ht_given(after_time), &
                                           latent_ht_given(before_time) )
  CO2_flx = factor_interp( time_frac, CO2_sfc_given(after_time), &
                                      CO2_sfc_given(before_time) )
  T_sfc = factor_interp( time_frac, T_sfc_given(after_time), &
                                    T_sfc_given(before_time) )

  ! Convert heat_flx and moisture_flx to natural units
  heat_flx2     = convert_sens_ht_to_km_s( heat_flx, rho_sfc )    ! (K m/s)
  moisture_flx2 = convert_latent_ht_to_m_s( moisture_flx, rho_sfc )! (m/s)

  !       Convert CO2 surface flux to natural units.
  !       The CO2 flux has been given in units of:  umol/(m^2 s).
  !       umol stands for micromoles.  The CO2 concentration in
  !       this code is in units of ppmv, which is also the molar
  !       mixing ratio times 10^6.
  !       The units are:  10^6 * [ mol (CO2) / mol (dry air) ].
  !       w'CO2' = (Flux) * [ M (dry air) / rho (dry air) ];
  !       where M is the molecular weight of dry air.
  CO2_flx2 = CO2_flx * ( M_da / rho_sfc )

  ! Heat flux in units of (m2/s3) (needed by diag_ustar)
  bflx = grav/thlm_sfc * heat_flx2

  ! Compute ustar
  ustar = diag_ustar( z, bflx, ubar, z0 )

  ! Assign fluxes

  wpthlp_sfc = heat_flx2
  wprtp_sfc  = moisture_flx2

  if ( iisclr_CO2 > 0 ) wpsclrp_sfc(iisclr_CO2) = CO2_flx2
  if ( iisclr_thl > 0 ) wpsclrp_sfc(iisclr_thl) = wpthlp_sfc
  if ( iisclr_rt  > 0 ) wpsclrp_sfc(iisclr_rt)  = wprtp_sfc

  if ( iiedsclr_CO2 > 0 ) wpedsclrp_sfc(iiedsclr_CO2) = CO2_flx2
  if ( iiedsclr_thl > 0 ) wpedsclrp_sfc(iiedsclr_thl) = wpthlp_sfc
  if ( iiedsclr_rt  > 0 ) wpedsclrp_sfc(iiedsclr_rt)  = wprtp_sfc

  return
  end subroutine cobra_sfclyr

end module cobra
