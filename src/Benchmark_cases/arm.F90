!----------------------------------------------------------------------
! $Id$
module arm

  !       Description:
  !       Contains subroutines for the GCSS ARM case.
  ! 
  !       References:
  !       Brown et al., 2002:  Large-eddy simulation of the diurnal cycle
  !       of shallow cumulus convection over land. Quart. J. Roy. 
  !       Meteor.  Soc., 128, 1075-1093. 
  !       http://www.atmos.washington.edu/~breth/GCSS/Brown_etal_
  !       DiurnalShCu_QJ2002.pdf
  !----------------------------------------------------------------------

  implicit none

  public :: arm_sfclyr

  private ! Default Scope

  contains

  !----------------------------------------------------------------------
  subroutine arm_sfclyr( time, z, rho_sfc, thlm_sfc, ubar,  & 
                         wpthlp_sfc, wprtp_sfc, ustar )

  !       Description:
  !       This subroutine computes surface fluxes of horizontal momentum,
  !       heat and moisture according to GCSS ARM specifications
  !   
  !       References:
  !       See module comments.
  !----------------------------------------------------------------------

  use constants_clubb, only: grav ! Variable(s)

  use clubb_precision, only: time_precision ! Variable(s)

  use diag_ustar_module, only: diag_ustar ! Variable(s)

  use surface_flux, only: compute_ht_mostr_flux, &
                          convert_sens_ht_to_km_s, convert_latent_ht_to_m_s ! Procedures


  implicit none

  intrinsic :: max, sqrt

  ! Constants
  real, parameter ::  & 
    z0 = 0.035  ! ARM Cu momentum roughness height
  integer, parameter :: &
    ntimes = 7

  ! Input variables
  real(kind=time_precision), intent(in) ::  & 
    time            ! Current time          [s]

  real, intent(in) ::  & 
    z,               & ! Height at zt(2)       [m]
    rho_sfc,             & ! Density at zm(1)      [kg/m^3]
    thlm_sfc,        & ! Theta_l at zt(2)      [K]
    ubar

  ! Output variables
 real, intent(out) ::  & 
    wpthlp_sfc,  & ! w'theta_l' surface flux   [(m K)/s]
    wprtp_sfc,   & ! w'rt' surface flux        [(m kg)/(kg s)]
    ustar          ! surface friction velocity [m/s]

  ! Local variables
  real ::  & 
    heat_flx, moisture_flx, & 
    heat_flx2, moisture_flx2, & 
    bflx


  !-----------------BEGIN CODE-------------------------

  ! Compute heat and moisture fluxes from ARM data in (W/m2)
   call compute_ht_mostr_flux( time, ntimes, &
                               heat_flx, moisture_flx )

  ! Compute momentum fluxes

  ! Convert heat_flx and moisture_flx to natural units
  heat_flx2     = convert_sens_ht_to_km_s(heat_flx, rho_sfc)    ! (K m/s)
  moisture_flx2 = convert_latent_ht_to_m_s(moisture_flx, rho_sfc) ! (m/s)

  ! Heat flux in units of (m2/s3) (needed by diag_ustar)
  bflx = grav/thlm_sfc * heat_flx2

  ! Surface winds

  ! Compute ustar
  ustar = diag_ustar( z, bflx, ubar, z0 )

  ! Assign fluxes

  wpthlp_sfc = heat_flx2
  wprtp_sfc  = moisture_flx2

  return
  end subroutine arm_sfclyr

end module arm
